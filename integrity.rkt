#lang racket/base

(require "contract.rkt")

; Mirror OpenSSL
(define xiden-hash-algorithms
  '(blake2b512
    blake2s256
    md4
    md5
    rmd160
    sha1
    sha1
    sha224
    sha256
    sha3-224
    sha3-256
    sha3-384
    sha3-512
    sha384
    sha512
    sha512-224
    sha512-256
    shake128
    shake256
    sm3))

(define xiden-hash-source/c
  (or/c path-string? bytes? input-port?))

(define xiden-hash-algorithm/c
  (apply or/c xiden-hash-algorithms))

(provide (struct-out integrity-info)
         (contract-out
          [integrity
           (-> xiden-hash-algorithm/c
               bytes?
               integrity-info?)]
          [xiden-hash-algorithms
           (non-empty-listof symbol?)]
          [xiden-hash-algorithm/c
           flat-contract?]
          [well-formed-integrity-info/c
           flat-contract?]
          [make-digest
           (-> xiden-hash-source/c
               xiden-hash-algorithm/c
               bytes?)]
          [make-fingerprint
           (-> path-string? bytes?)]
          [passed-integrity-check?
           predicate/c]
          [check-integrity
           (-> #:trust-bad-digest any/c
               any/c
               xiden-hash-source/c
               (unconstrained-domain-> $integrity?))]))

(require racket/sequence
         racket/format
         "codec.rkt"
         "file.rkt"
         "message.rkt"
         "openssl.rkt")

(define+provide-message $integrity           (input-name input-source))
(define+provide-message $integrity:verified  $integrity ())
(define+provide-message $integrity:missing   $integrity ())
(define+provide-message $integrity:unchecked $integrity ())
(define+provide-message $integrity:violation $integrity ())

(struct integrity-info (algorithm digest) #:prefab)

(define integrity integrity-info)

(define (digest-length-ok? info)
  (equal? (bytes-length (integrity-info-digest info))
          (bytes-length (make-digest #"whatever"
                                     (integrity-info-algorithm info)))))


(define (make-fingerprint path)
  (subbytes (make-digest path 'sha384) 0 20))


(define well-formed-integrity-info/c
  (and/c (struct/c integrity-info
                   xiden-hash-algorithm/c
                   bytes?)
         digest-length-ok?))


(define (make-integrity-info variant algorithm)
  (integrity-info algorithm (make-digest variant algorithm)))


(define (make-digest variant algorithm)
  (cond [(path-string? variant)
         (call-with-input-file variant (Î» (i) (make-digest i algorithm)))]
        [(bytes? variant)
         (make-digest (open-input-bytes variant) algorithm)]
        [(input-port? variant)
         (run-openssl-command variant
                              "dgst"
                              "-binary"
                              (~a "-" algorithm))]
        [else (raise-argument-error 'make-digest
                                    "A path, bytes, or an input port"
                                    variant)]))


(define (check-integrity #:trust-bad-digest trust-bad-digest intinfo variant)
  (if trust-bad-digest
      $integrity:unchecked
      (if (well-formed-integrity-info/c intinfo)
          (if (equal? (integrity-info-digest intinfo)
                      (make-digest variant (integrity-info-algorithm intinfo)))
              $integrity:verified
              $integrity:violation)
          $integrity:missing)))

(define (passed-integrity-check? m)
  (and (member m (list $integrity:verified
                       $integrity:unchecked))
       #t))


(module+ test
  (require rackunit)

  (test-case "Create integrity information"
    (for ([algorithm (in-list xiden-hash-algorithms)])
      (define bstr (string->bytes/utf-8 (symbol->string algorithm)))
      (define info (make-integrity-info bstr algorithm))
      (check-pred integrity-info? info)
      (check-eq? (integrity-info-algorithm info) algorithm)
      (check-equal? (integrity-info-digest info)
                    (make-digest bstr algorithm)))))
